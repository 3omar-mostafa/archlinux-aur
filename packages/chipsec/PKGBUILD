# Maintainer: ArchStrike <team archstrike org>
# Contributor: CHIPSEC team <chipsec@intel.com>

pkgname=chipsec
pkgver=1.12.0
pkgrel=1
pkgdesc="A framework for analyzing security of PC platforms including hardware, system firmware including BIOS/UEFI and the configuration of platform components."
arch=('i686' 'x86_64' )
url='https://github.com/chipsec/chipsec'
license=('GPL2')
depends=('python' 'nasm' 'dkms')
makedepends=('python-setuptools' 'gcc' 'libelf' 'make')
source=("${url}/archive/${pkgver}.tar.gz"
        "setup.patch")
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -Np1 --ignore-whitespace -i "$srcdir/setup.patch"
  
  # Kernel modules in arch linux is using .zst extension instead of .xz
  sed -i 's/.xz/.zst/g' chipsec/helper/linux/linuxhelper.py
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -dm755 "${pkgdir}/usr/src/${pkgname}-${pkgver}/amd64"
  install -dm755 "${pkgdir}/usr/src/${pkgname}-${pkgver}/i386"
  install -dm755 "${pkgdir}/usr/src/${pkgname}-${pkgver}/include"
  
  install -Dm644 drivers/linux/chipsec_km.c "${pkgdir}/usr/src/${pkgname}-${pkgver}/chipsec_km.c"
  install -Dm644 drivers/linux/dkms.conf "${pkgdir}/usr/src/${pkgname}-${pkgver}/dkms.conf"
  install -Dm644 drivers/linux/Makefile "${pkgdir}/usr/src/${pkgname}-${pkgver}/Makefile"
  install -Dm644 drivers/linux/amd64/cpu.asm "${pkgdir}/usr/src/${pkgname}-${pkgver}/amd64/cpu.asm"
  install -Dm644 drivers/linux/i386/cpu.asm "${pkgdir}/usr/src/${pkgname}-${pkgver}/i386/cpu.asm"
  install -Dm644 drivers/linux/include/chipsec.h "${pkgdir}/usr/src/${pkgname}-${pkgver}/include/chipsec.h"
  
  sed -e "s/VERSION.*$@/VERSION=${pkgver}/" -i "${pkgdir}"/usr/src/${pkgname}-${pkgver}/dkms.conf

  python setup.py install --prefix=/usr --root="${pkgdir}" -O1

  mv "${pkgdir}/usr/chipsec-manual.pdf" "${pkgdir}/usr/share/doc/${pkgname}/"
}
