pkgname=apk-editor-studio
pkgver=1.7.0
pkgrel=1
pkgdesc="APK Editor Studio is a powerful yet easy-to-use APK reverse-engineering tool"
arch=('x86_64')
url='https://qwertycube.com/apk-editor-studio'
license=('GPL3')
depends=('qt5-base'
         'java-runtime>=8'
         'hicolor-icon-theme'
         'android-sdk-build-tools'
         'android-sdk-platform-tools'
         'android-apktool'
         'libsecret')
makedepends=('cmake' 'ninja' 'qt5-base' 'qt5-tools' 'libsecret')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kefir500/apk-editor-studio/archive/v${pkgver}.tar.gz"
        "disable-translations.patch")
sha256sums=('c656e7f934b042b75b1e04931adb759fefe4b86e71714f63f5e8f65ed2257c6a'
            '3b6bb145ee8f6072e7f94a860407436e07fc8390cf8b93dd9adb402c1b0da4fd')

build() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 --ignore-whitespace -i "$srcdir/disable-translations.patch"

    mkdir -p ../build
    cd "$srcdir/build"
    cmake ../"${pkgname}-${pkgver}" \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_PREFIX_PATH=/usr \
    -DBUILD_TRANSLATIONS=OFF \
    -Wno-dev

    ninja
}

package() {
    cd "$srcdir/build"
    DESTDIR="$pkgdir" ninja install
}

