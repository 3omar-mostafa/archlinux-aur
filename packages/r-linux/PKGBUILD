# Maintainer: mrxx <mrxx at cyberhome dot at>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=r-linux
pkgver=6.2.191150
pkgrel=1
pkgdesc="File recovery utility for the ext2/ext3/ext4 file system"
arch=('i686' 'x86_64')
url="https://www.r-tt.com/data_recovery_linux/"
license=('custom')
depends=('xorg-xhost')
optdepends=('gksu' 'kdesu' 'polkit')
makedepends=('dpkg')
source_i686=("$pkgname-i386.deb::https://www.r-tt.com/downloads/RLinux6_i386.deb")
source_x86_64=("$pkgname-x64.deb::http://www.r-tt.com/downloads/RLinux6_x64.deb")
source=(rlinux.sh)
sha512sums=('01ea9c642f46a35a9b90896a7b93c3b03a5256100d6a1443286a5da58fcdca0c49447a48a2a6c335ab334cb53965ed4269381ecdda94af39d2f3c8542ce2cbae')
sha512sums_i686=('SKIP')
sha512sums_x86_64=('SKIP')
noextract=("$pkgname-i386.deb" "$pkgname-x64.deb" )


pkgver() {
  dpkg-deb -f $pkgname-*.deb Version
}

prepare() {
    dpkg-deb --extract $pkgname-*.deb .
}

package() {
  install -d "$pkgdir/opt"
  install -d "$pkgdir/usr/share/applications"

  install -Dm 755 rlinux.sh "$pkgdir"/usr/bin/rlinux
  cp -r usr/local/R-Linux "$pkgdir"/opt/

  cd usr/local/R-Linux/share
  install -Dm 644 logo_48.png "$pkgdir"/usr/share/pixmaps/rtt-rlinux.png
  sed 's|Exec=.*|Exec=rlinux|' rtt-rlinux.desktop >"$pkgdir"/usr/share/applications/rtt-rlinux.desktop
  sed -i 's|/usr/local|/opt|' "$pkgdir"/opt/R-Linux/bin/rlinux
}
