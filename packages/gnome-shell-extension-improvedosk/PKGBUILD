# Maintainer: Sebastian Wiesner <sebastian@swsnr.de>

pkgname=gnome-shell-extension-improvedosk
pkgver=18
# Upstream has no tags, so we refer to the commit which bumped the version
_commit='b67e307a475d4d9db0f6f50d868447f961a27be4'
pkgrel=1
pkgdesc="Makes GNOME's onscreen keyboard more useable."
arch=('any')
url='https://github.com/nick-shmyrev/improved-osk-gnome-ext'
license=('GPL3')
depends=('gnome-shell')
makedepends=('unzip' 'jq')
optdepends=('ttf-symbola: Recommended font for symbols')
source=("$pkgname-$pkgver.tar.gz::$url/archive/${_commit}.tar.gz")
sha512sums=('SKIP')
_uuid='improvedosk@nick-shmyrev.dev'

build() {
    cd "improved-osk-gnome-ext-${_commit}" || exit
    glib-compile-resources src/data/gnome-shell-osk-layouts.gresource.xml \
        --sourcedir=src/data/osk-layouts --target=src/data/gnome-shell-osk-layouts.gresource
    glib-compile-schemas src/schemas
}

package() {
    cd "improved-osk-gnome-ext-${_commit}" || exit

    # Patch to support osk in login screen
    cat <<< $(jq '."session-modes"=["user","unlock-dialog"]' ./src/metadata.json) > ./src/metadata.json

    local extdir="${pkgdir}/usr/share/gnome-shell/extensions/${_uuid}/"
    install -d -m755 "${extdir}"/{schemas,data}
    install -m644 -t "${extdir}/schemas" \
        src/schemas/gschemas.compiled src/schemas/org.gnome.shell.extensions.improvedosk.gschema.xml
    install -m644 -t "${extdir}/data" src/data/gnome-shell-osk-layouts.gresource
    install -m644 -t "${extdir}" src/*.js src/metadata.json
}
