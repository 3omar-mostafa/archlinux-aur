pkgname=gnome-shell-extension-net-speed-simplified
_pkgname=netspeedsimplified
pkgver=39
pkgrel=2
pkgdesc="A Net Speed extension With Loads of Customization. Fork of simplenetspeed"
arch=('any')
url="https://www.github.com/prateekmedia/netspeedsimplified"
license=('GPL')
depends=('gnome-shell')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "nordvpn.patch")
sha256sums=('f651f87d1ea3a65e31fecb82f162a3e24ee328f1bdea533ca23e06efb939bc98'
            '1b0fcca42cf59cf1b0cbff00a996d1b6f30813c585bddca09efbba1e9b040869')

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    patch -Np1 --ignore-whitespace -i "$srcdir/nordvpn.patch"
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    local _uuid=$(grep -Po '(?<="uuid": ")[^"]*' metadata.json)
    install -d "${pkgdir}/usr/share/gnome-shell/extensions/${_uuid}"
    cp -a * "${pkgdir}/usr/share/gnome-shell/extensions/${_uuid}"
}
